plugins {
    id 'java-library'
    id "me.champeau.jmh" version "0.7.0"
    id 'com.github.johnrengelman.shadow'  version '8.1.1'
    id "idea"
}

repositories {
    mavenCentral()
}

tasks.register('copyTestData', Copy) {
    description = "Copies TestData into the JMH build directory for benchmark execution"
    group = "benchmarking"

    from project.rootProject.file("TestData")
    into layout.buildDirectory.dir("TestData")
}

tasks.named("assemble") {
    dependsOn(tasks.named("copyTestData"))
    dependsOn shadowJar
}

sourceSets {
    jmh {
        resources {
            srcDir "$buildDir/jmh-generated-resources"
        }
    }
}

shadowJar {
    archiveBaseName.set("JMH")
    archiveClassifier.set("all")
    archiveVersion.set("")

    configurations = [
            project.configurations.jmhRuntimeClasspath
    ]

    from(sourceSets.jmh.output)
}


dependencies {
    testImplementation project(path: ':Configuration-Creator')
    testImplementation project(path: ':Configuration-Manager')
    testImplementation project(path: ':Canete')

    jmh 'org.openjdk.jmh:jmh-core:1.37'
    jmh 'org.openjdk.jmh:jmh-generator-annprocess:1.37'

    // this is the line that solves the missing /META-INF/BenchmarkList error
    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.37'

    implementation 'org.apache.logging.log4j:log4j-api:2.24.3'
    implementation 'org.apache.logging.log4j:log4j-core:2.24.3'

    implementation project(':Shared')
}